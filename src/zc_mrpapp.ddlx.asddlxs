@Metadata.layer: #CORE

@UI: { headerInfo: { title:          { type: #STANDARD,
                                       label: 'Application',
                                       value: 'AppName' } } }

annotate entity ZC_MRPAPP with
{

  @UI.facet:
  [
      {
        purpose: #STANDARD,
        type: #COLLECTION,
        position: 10,
        label: 'Parameters',
        id: 'SelectionScreen'
      },
      {
        purpose: #STANDARD,
        type: #IDENTIFICATION_REFERENCE,
//        label: 'Parameters',
        id: 'Parameters',
        position: 10,
        parentId: 'SelectionScreen'
      },
      {
        purpose: #STANDARD,
        type: #LINEITEM_REFERENCE,
        id: 'Materials',
        parentId: 'SelectionScreen',
        position: 20,
        targetElement: '_Materials'
       },
       {
        purpose: #STANDARD,
        type: #LINEITEM_REFERENCE,
        id: 'MRPArea',
        parentId: 'SelectionScreen',
        position: 30,
        targetElement: '_MRP'
       },
       {
        id         : 'Output',
        purpose    : #STANDARD,
        position   : 20,
        label      : 'Result',
        type       :  #LINEITEM_REFERENCE,
        targetElement: '_Output'
        }
  ]

  @UI.hidden: true
  Uname;

  @UI.lineItem      : [{ position: 10, label: 'Application' }]
  @UI.selectionField: [{exclude: true}]
  AppName;

  @EndUserText.label: 'Plant'
  @UI.lineItem: [{hidden: true}]
  @UI.identification: [ { position: 10 },
                        { type: #FOR_ACTION, label: 'Clear', dataAction: 'clearInput' },
                        { type: #FOR_ACTION, label: 'Export to Excel', dataAction: 'exportToExcel' },
                        { type: #FOR_ACTION, label: 'Extract data', dataAction: 'extractData' } ]
  @UI.selectionField: [{exclude: true}]
  Plant;

  @EndUserText.label: 'Region'
  @UI.identification: [{ position: 20 }]
  @UI.lineItem: [{hidden: true}]
  @UI.selectionField: [{exclude: true}]
  Region;

  @EndUserText.label: 'Perform update?'
  @UI.identification: [{ position: 30 }]
  @UI.lineItem: [{hidden: true}]
  @UI.selectionField: [{exclude: true}]
  Updatedata;
  
  @EndUserText.label: 'Download file'
  @UI.lineItem: [{hidden: true}]
  @UI.selectionField: [{exclude: true}]
  @UI.identification: [{ position: 40 }]
  Attachment;

}